#!/usr/local/bin/perl -w
###########################################
# copyright-patch
###########################################
use strict;
use Sysadm::Install qw(:all);
use Log::Log4perl qw(:easy);
use File::Find;
use File::Basename;

Log::Log4perl->easy_init($DEBUG);

my $copyright = <<'EOT';
# Copyright (c) 2010-2011 Yahoo! Inc. All rights reserved.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
EOT

find( sub {
    my $file = $_;

    return if basename($file) eq basename($0);

    return if ! -T $file;
    file_patch( $file, $copyright );

}, ".");

###########################################
sub file_patch {
###########################################
    my( $file, $copyright ) = @_;

    my $data = slurp( $file );

    if( !$data ) {
        LOGWARN "Failed to read $file ($!)";
        return;
    }

    $data =~ s/^# Copyright \(c\).*?(?=^[^#])/$copyright/msg;

    blurt $data, $file;
}
